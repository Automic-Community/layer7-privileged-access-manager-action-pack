*** Settings ***
Suite Setup       Startup
Suite Teardown    Teardown
Force Tags        GET CREDENTIALS
Test Template     GET Credentials Template
Library           com.automic.robot.itpa.ItpaLibrary
Library           String
Library           DateTime
Resource          ../../Resources/messages.txt
Resource          ../../Resources/keywords.txt
Resource          ../../Resources/variables.txt
Resource          ../../Resources/actions.txt

*** Test Cases ***    OK_NOK    CAPAMPath         Alias       Encrypt
GET CREDENTIALS when mandatory inputs have valid data and Encrypt is YES
                      [Tags]    main_scenarios
                      OK        ${CAPAM_Path}     ${alias}    YES

GET CREDENTIALS when mandatory inputs have valid data and Encrypt is NO
                      OK        ${CAPAM_Path}     ${alias}    NO

GET CREDENTIALS with invalid CAPAM Path
                      NOK       Invalid           ${alias}    NO

GET CREDENTIALS with invalid Alias
                      NOK       ${CAPAM_Path}     Invalid     NO

GET CREDENTIALS with empty CAPAM_Path
                      NOK       ${Empty}          ${alias}    NO

GET CREDENTIALS with empty Alias
                      NOK       ${CAPAM_Path}     ${Empty}    NO

*** Keywords ***
Startup
    Log    *******Start Test "GET CREDENTIALS"*******
    Log    *******Connect to AE*******
    Connect AE

GET Credentials Template
    [Arguments]    ${OK_NOK}    ${CAPAMPath}    ${Alias}    ${Encrypt}
    [Documentation]    This action tests the feature of GET CREDENTIALS.
    ...    -${OK_NOK}: Boolean value indicate if the action is ENDED_OK or ENDED_NOT_OK. Value of this variable should be OK or NOK.
    ...    -${CAPAMPath}: This field specify the Path of CSPM folder. E.g.: C:\cspm\cloakware
    ...    -${Alias}: This field specify Unique Name of Alias which you give at the time of create account. E.g.: testAlias
    ...    -${Encrypt}: This field specify whether if retrieved SECRETS should be kept as clear text or should be encrypted in CDA for its security.
    Generate String
    Init Action    ${_GET_CREDENTIALS_ACTION}
    Run Keyword If    '${CAPAMPath}' == 'Invalid'    Action Set    &UC4RB_PAM_PATH#    ${Generated_Name}_${Time}
    ...    ELSE    Action Set    &UC4RB_PAM_PATH#    ${CAPAMPath}
    Run Keyword If    '${Alias}' == 'Invalid'    Action Set    &UC4RB_PAM_ALIAS#    ${Generated_Name}_${Time}
    ...    ELSE    Action Set    &UC4RB_PAM_ALIAS#    ${Alias}
    Action Set    &UC4RB_PAM_ENCRYPT#    ${Encrypt}
    Action Execute
    Run Keyword If    '${OK_NOK}' == 'OK'    Action Object Variable Should Be Found    &UC4RB_PASSWORD#
    ...    ELSE    Action Object Variable Should Not Be Found    &UC4RB_PASSWORD#

Teardown
    Log    *******End Test "GET CREDENTIALS"*******
